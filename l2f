#!/usr/bin/awk -f

# rely on values initialized to 0 (fields, colwidth, data)
function parseline(line) {
  if (getline <= 0) return;
  if (NF > fields) fields = NF;

  for (c = 1; c <= NF; c++) {
    if (length($c) > colwidth[line+1])
      colwidth[line+1] = length($c);
    data[line,c] = $c;
  }
}

BEGIN {
  # setup from environment
  ##########################
  if (ENVIRON["D"] != "")
    FS=ENVIRON["D"]
  else
    FS=",";

  if (ENVIRON["S"] != "")
    skip_lines = ENVIRON["S"]
  else
    skip_lines = 0

  if (ENVIRON["L"] != "")
    num_lines = ENVIRON["L"]
  else
    num_lines = 1

  ###########################
  parseline(0) # header
  for (i = 0; i < skip_lines; i++)
    if (getline <= 0) break;
  for (l = 1; l <= num_lines; l++) {
    parseline(l)
  }
  num_lines = l; # just in case there were fewer lines

  for (c = 1; c <= fields; c++) {
    printf "%3i: ", c;
    for (l = 0; l < num_lines; l++) {
      printf "|%-*.*s", colwidth[l+1]+3, colwidth[l+1], data[l,c];
    }
    printf "\n";
  }
}
